This program allows the player as a Gamma (default unit type level 3 - change
 player_unit to yours) to control a bunch of Novas (level 1 ground units).
The Novas will boost (fly) and follow the player if they are too far (more than
 30 blocks distance). When the player clicks a location, the Novas will approach
 that location and shoot there as soon as they are in range. If they are already
 in range they will only attack, but if they are in the range of a dangerous
 tower (towers that attack ground targets) they will shoot from as far away as
 possible, so you can attack a tower while making sure the Novas don't go under
 fire.

If the player is in control of a unit or group of units, the Novas will just
 stay still until the player is back.
Can be turned on/off using a switch nearby. When off the units only follow the
 command center.
Logic processor or higher is recommended.

To start, place a switch near the processor and link it as switch1 and turn it on.

COPY THE CODE BELOW TO THE PROCESSOR:

set player_unit @gamma
sensor on switch1 @enabled
jump 1 equal on false
ubind player_unit
sensor x @unit @x
sensor y @unit @y
sensor shooting @unit @shooting
sensor shootX @unit @shootX
sensor shootY @unit @shootY
ubind @nova
sensor boosting @unit @boosting
ucontrol within x y 30 within 0
jump 16 equal within true
jump 17 equal shooting true
jump 16 strictEqual x null
ucontrol approach x y 5 0 0
jump 47 equal within false
sensor range @unit @range
jump 47 equal shooting false
ulocate building turret true @copper outx outy found building
jump 38 equal found false
sensor tower_type building @type
sensor tower_range building @range
sensor tower_x building @x
sensor tower_y building @y
op equal result tower_type @duo
op equal result1 tower_type @hail
op or result result result1
op equal result1 tower_type @lancer
op or result result result1
op equal result1 tower_type @salvo
op or result result result1
op equal result1 tower_type @ripple
op or result result result1
op equal result1 tower_type @fuse
op or result result result1
ucontrol within tower_x tower_y tower_range tower_nearby 0
op land in_danger result tower_nearby
ucontrol within shootX shootY range near_enough 0
jump 41 equal boosting false
ucontrol move shootX shootY range 0 0
jump 46 equal in_danger true
jump 45 equal near_enough false
jump 45 equal boosting true
ucontrol stop shootX shootY range 0 0
jump 47 equal near_enough true
ucontrol approach shootX shootY range 0 0
ucontrol target shootX shootY shooting 0 0
jump 53 strictEqual x null
op equal not_within within false
op equal not_shooting shooting false
op land boost not_within not_shooting
ucontrol boost boost 0 0 0 0
end


